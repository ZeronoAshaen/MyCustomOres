buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'java'
apply plugin: 'forge'
apply plugin: 'curseforge'
apply plugin: 'maven'

ext.configFile = file "build.properties"

configFile.withReader {
	def prop = new Properties()
	prop.load(it)
	project.ext.config = new ConfigSlurper().parse prop
}

version = config.mod_version
group= "co.zerono.mco"
archivesBaseName = "MyCustomOres"

minecraft {
    version = config.minecraft_version + "-" + config.forge_version
	
	replaceIn "reference/Reference.java"
	replace "@VERSION@", "${config.mod_version}.${System.getenv().BUILD_NUMBER ?: 0}"
	if (project.hasProperty("mco_signature")) {
		replace "@FINGERPRINT@", project.mco_signature
	}
    //runDir = "eclipse"
}

version = "${config.minecraft_version}-${config.mod_version}.${config.build_number}"

processResources
{
	// exclude xcf files, as they are for development only
	exclude '**/*.xcf'
	
    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include '*.info'
                
        // replace version and mcversion
        expand 'mod_version':project.version, 'minecraft_version':project.config.minecraft_version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude '**/*.info'
    }
}

task devJar(type: Jar, dependsOn: 'classes') {
    from(sourceSets.main.output) {
        include '**'
    }

    extension = 'jar'
    classifier = 'dev'
}

artifacts {
    archives devJar
}

curse {
	apiKey = config.curseForgeApiKey
    projectId = config.curseForgeProjectId
    releaseType = config.release_type
	changelog = config.change_log
}